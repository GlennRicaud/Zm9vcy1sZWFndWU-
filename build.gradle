plugins {
    id 'java'
    id 'com.enonic.xp.app' version '1.0.11'
    id 'com.eriwen.gradle.js' version '2.12.0'
}

xp {
    version = xpVersion
}

app {
    name = project.ext.appName
    displayName = project.ext.displayName
}

configurations {
    rcdjsfwkcore
    rcdjsfwkmaterial
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.lib:lib-router:1.0.0"
    rcdjsfwkcore "systems.rcd.js.fwk.core:rcd-jsfwk-core:3.0.0-alpha-3"
    rcdjsfwkmaterial "systems.rcd.js.fwk.material:rcd-jsfwk-material:3.0.0-alpha-2"
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven {
        url "http://dl.bintray.com/rcd-systems/rcd-repo"
    }
}

task extractRcdJsFwk( type: Copy ) {
    from { configurations.rcdjsfwkcore.collect { zipTree( it ); } }
    from { configurations.rcdjsfwkmaterial.collect { zipTree( it ); } }
    into "${rootProject.buildDir}/resources/main/"
    exclude 'META-INF/**'
}
processResources.dependsOn extractRcdJsFwk

//TODO Replace by proper plugin
task lessc( dependsOn: processResources ) << {
    exec {
        executable 'lessc'
        args 'build/resources/main/assets/style/main.less',
             'build/resources/main/assets/style/main.css'
    }
}
jar.dependsOn lessc


task mergeJs( type: com.eriwen.gradle.js.tasks.CombineJsTask ) {
    source = ["${rootProject.buildDir}/resources/main/assets/js/graphql/graphql.js",
              "${rootProject.buildDir}/resources/main/assets/js/common/panel.js",
              "${rootProject.buildDir}/resources/main/assets/js/common/button.js",
              "${rootProject.buildDir}/resources/main/assets/js/common/image.js",
              "${rootProject.buildDir}/resources/main/assets/js/common/items.js",
              "${rootProject.buildDir}/resources/main/assets/js/game/game-side.js",
              "${rootProject.buildDir}/resources/main/assets/js/game/game-panel.js",
              "${rootProject.buildDir}/resources/main/assets/js/game/games-panel.js",
              "${rootProject.buildDir}/resources/main/assets/js/game/game-helper.js",
              "${rootProject.buildDir}/resources/main/assets/js/league/ranking.js",
              "${rootProject.buildDir}/resources/main/assets/js/league/player-ranking.js",
              "${rootProject.buildDir}/resources/main/assets/js/league/team-ranking.js",
              "${rootProject.buildDir}/resources/main/assets/js/league/leagues.js",
              "${rootProject.buildDir}/resources/main/assets/js/league/league.js",
              "${rootProject.buildDir}/resources/main/assets/js/app.js"]
    dest = file( "${rootProject.buildDir}/resources/main/assets/js/main.js" )
}
jar.dependsOn mergeJs
